*** Settings ***
Documentation    Cenarios de cadastro de tarefas

Test Setup        Start Session
Test Teardown     Take Screenshot

Resource    ../../resources/base.resource

Library    JSONLibrary

*** Test Cases ***
Deve poder cadastrar uma nova tarefa
    [Tags]    critical

    ${data}    Get fixture    tasks    create

    Reset user from database     ${data}[user]

    Do login    ${data}[user]

    Go to task form
    Submit task form    ${data}[task]
    Task should be registered    ${data}[task][name]

Não deve cadastrar tarefa com nome duplicado
    [Tags]    dup

    ${data}    Get fixture    tasks    duplicate

    #Dado que eu tenho um novo usuario
    Reset user from database     ${data}[user]

    #E que esse usuario ja cadastrou uma tarefa
    Create a new task from API    ${data}
    
    #E que estou logado na aplicação web
    Do login    ${data}[user]
    
    #Quando faço um cadastro dessa mesma tarefa que foi cadastrada
    Go to task form
    Submit task form    ${data}[task]

    #Então deve ver uma notificação de duplicidade
    Notice should be    Oops! Tarefa duplicada.    

Não deve cadastrar uma nova tarefa quando atinge o limite de tags
    [Tags]    tags_limit

    ${data}    Get fixture    tasks    tags_limit
    
    Reset user from database     ${data}[user]

    Do login    ${data}[user]
    
    Go to task form
    Submit task form    ${data}[task]

    Notice should be    Oops! Limite de tags atingido.

